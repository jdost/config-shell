# custom prompt theme

prompt_custom_setup () {
  base_prompt="%F{green}%n%F{yellow}@%F{green}%U%m%u%F{red}:%f"
  base_prompt_nc=$(echo "$base_prompt" | perl -pe "s/%(K{.*?}|k|F{.*?}|f|U|u)//g")
  post_prompt="%f%k"

  add-zsh-hook precmd prompt_custom_precmd
}

prompt_custom_precmd () {
  setopt noxtrace localoptions
  local base_prompt_expanded_no_color base_prompt_etc
  local prompt_length space_left

  base_prompt_expanded_no_color=$(print -P "$base_prompt_nc")
  base_prompt_etc=$(print -P "$base_prompt%(4~|...|)%3~")
  prompt_length=${#base_prompt_etc}
  if [[ $prompt_length -lt 40 ]]; then
    path_prompt="%F{white}%(4~|...|)%3~ %K{white}%F{black}"
  else
    space_left=$(( $COLUMNS - $#base_prompt_expanded_no_color - 2 ))
    path_prompt="%F{white}%${space_left}<...<%~ %K{white}%F{black}"
  fi
  PS1="$base_prompt$path_prompt%#$post_prompt "
  PS2="$base_prompt$path_prompt%_>$post_prompt "
  PS3="$base_prompt$path_prompt?#$post_prompt "
}

prompt_custom_setup "$@"

# vim: ft=zsh
